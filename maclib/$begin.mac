.*
.*
.*  This program and the accompanying materials are
.*
.*  made available under the terms of the Eclipse Public License v2.0 which accompanies
.*
.*  this distribution, and is available at https://www.eclipse.org/legal/epl-v20.html
.*
.*  
.*
.*  SPDX-License-Identifier: EPL-2.0
.*
.*  
.*
.*  Copyright Contributors to the Zowe Project.
.*
.*
.**********************************************************************
.* Macro: $BEGIN
.*
.* Description:
.*   Generate assembler prolog/entry code
.*
.* Note:
.*   Jump mnemonics are documented in HL-ASM Language Reference
.*   (SC26-4940), Release 3 and newer
.*
.**********************************************************************
         MACRO
&NAME    $BEGIN &AMODE=31,             Default AMODE                   >
               &ENTRY=NO,              Entry section YES|NO            >
               &RMODE=ANY,             Default RMODE                   >
               &BASE=12,               Default base                    >
               &BASENUM=1,             Default number of base regs     >
               &STDSAVE=YES,           Use R13 std save area           >
               &STACK=YES              Use linkage stack

.*
         PUSH  PRINT                   Save PRINT settings
         PRINT OFF,NOPRINT
         YREGS ,                       Reg equates
         AREGS ,                       Access Reg equates
         COPY  $BMCGLBL                CFG globals
         POP   PRINT                   Restore PRINT settings
.*
         LCLA  &BASE02
         LCLA  &N
&N       SETA  &SYSNDX
.*
         AIF   (&CFG$BEG).MERR030      Multiple use not supported
         AIF   ('&NAME' EQ '').MERR010
&NAME    AMODE &AMODE
&NAME    RMODE &RMODE
&NAME    RSECT ,
         AIF   ('&ENTRY' NE 'YES').MLBL010
         ENTRY &NAME
.MLBL010 ANOP  ,
         AIF   ('&AMODE' NE '64').MLBL020
&CFGAM64 SETB  1                       Set our global AMODE=64 flag
.MLBL020 ANOP  ,
         AIF   ('&STDSAVE' NE 'YES').MLBL200
         AIF   ('&AMODE' NE '64').MLBL100
         STMG  14,12,8(13)             Save "grande" regs
         AGO   .MLBL200
.MLBL100 ANOP  ,
         STM   14,12,12(13)            Save regs in std save area
.MLBL200 ANOP  ,
         AIF   ('&STACK' NE 'YES').MLBL300
         AIF   ('&AMODE' NE '64').MLBL220
         BAKR  14,0                    Put regs, status etc. on stack
         AGO   .MLBL300
.MLBL220 ANOP  ,
         BSM   14,0                    Save callers amode
         BAKR  14,0                    Put regs, status etc. on stack
         AIF   ('&AMODE' EQ '24').MLBL300
         USING &NAME,15                Temp addressabiity
         LA    14,BMCGO&N
         O     14,=X'80000000'         Ensure AMODE=31
         BSM   14,0                    Set our addressing mode
         DROP  15
.MLBL300 ANOP  ,
         J     BMCGO&N
BMCPROG  DC    CL8'&NAME'              Program name
BMCDATE  DC    CL8'&SYSDATE'           Assembly date
BMCTIME  DC    CL8'&SYSTIME'           Assembly time
BMCPVER  DC    CL2'&CFGVV'             Product version
BMCPREL  DC    CL2'&CFGRR'             Product release
BMCPMOD  DC    CL2'&CFGMM'             Modification level
BMCCPYR  DC    C' &CPYRGHT'
BMCGO&N  DS    0H
         AIF   ('&AMODE' EQ '64').MLBL400
         LR    &BASE,15                Establish base
         AIF   ('&BASENUM' EQ '1').BASE1
         AIF   ('&BASENUM' EQ '2').BASE2
         AGO   .MERR020
.BASE1   ANOP  ,
         USING &NAME,&BASE
         AGO   .MLBL500
.BASE2   ANOP  ,
&BASE02  SETA  &BASE-1
         USING &NAME,&BASE.,&BASE02
         LA    &BASE02.,2048(,&BASE)
         LA    &BASE02.,2048(,&BASE02)
         AGO   .MLBL500
.MLBL400 ANOP  ,
         LLGTR &BASE,15                Establish base
         NILL  &BASE,X'FFFE'           Turn off low order bit
         USING &NAME,&BASE
.MLBL500 ANOP  ,
&CFG$BEG SETB  1                       Indicate $BEGIN issued
         MEXIT ,
.**********************************************************************
.* Macro Error Messages
.**********************************************************************
.MERR010 ANOP  ,
         MNOTE 8,'$BEGIN - Positional NAME value is missing or invalid'
         MEXIT ,
.MERR020 ANOP  ,
         MNOTE 8,'$BEGIN - Macro supports a max of two base regs'
         MEXIT ,
.MERR030 ANOP  ,
         MNOTE 8,'$BEGIN - Cannot be issued multiple times'
         MEND  ,
